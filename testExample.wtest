import wollok.game.*
import objectsAndDragons.*      
import combateYUI.*             
import stages.*                 

describe "Unitarios - Dominio RPG" {

  test "recibirDaño nunca baja de 0 y el personaje queda no-vivo" {
    const p = new Personaje(position = game.center())

    p.recibirDaño(9999)

    assert.equals(0, p.vida())
    assert.equals(false, p.estaVivo())
  }

  test "ganarExp → subirNivel rellena vida/maná y sube stats" {
    const h = new Personaje(position = game.center())
    const vidaPrev = h.vidaMaxima()
    const manaPrevActual = h.mana()        

    h.ganarExp(h.expSiguienteNivel())     

    assert.equals(vidaPrev + 20, h.vidaMaxima())
    assert.equals(h.vidaMaxima(), h.vida())
    assert.equals(manaPrevActual + 10, h.mana())   
  }

  test "HabilidadAtaque respeta defensa y pega mínimo 1 (P vs P)" {
  const lanzador = new Personaje(position = game.center())
  const objetivo = new Personaje(position = game.center())
  const hab = new HabilidadAtaque(nombre="Golpecito", danio=-999, tipoDanio="fisico", costoMana=0)

  lanzador.usarHabilidad(hab, objetivo)

  assert.equals(objetivo.vidaMaxima() - 1, objetivo.vida())
}

  test "Costo de maná: si no alcanza, no aplica y no gasta" {
    const h = new Personaje(position = game.center())  
    const e = new Personaje(position = game.center())  

    const vidaAntes = e.vida()
    const manaAntes = h.mana()
    const mega = new HabilidadAtaque(nombre="MegaHechizo", danio=50, tipoDanio="magico", costoMana=999)

    h.usarHabilidad(mega, e)

    assert.equals(vidaAntes, e.vida())
    assert.equals(manaAntes, h.mana())
  }

  test "Poción: cura 30 y respeta tope en vidaMaxima" {
    const h = new Personaje(position = game.center())
    h.vida(50)
    const p = new Pocion(nombre="Poción 30", curacion=30)

    p.usar(h)
    assert.equals(80, h.vida())

    p.usar(h) // 80 + 30 -> cap en 100
    assert.equals(100, h.vida())
  }

  test "Portal simple: validar condición true/false sin romper" {
    const h = new Personaje(position = game.center())
    const portalLibre = new Portal(position=game.at(0,0), destino=bosqueDeMonstruos)
    const portalBloq  = new Portal(position=game.at(0,0), destino=salaDelBoss, condicion={ _ => false })

    mundo.cambiarArea(puebloDelRey)
    portalLibre.fueTocadoPor(h)   // debería cambiar de área internamente

    mundo.cambiarArea(puebloDelRey)
    portalBloq.fueTocadoPor(h)    // NO debería cambiar (condición false)
    assert.equals(true, true)     // marcador neutro (el valor es el no-crash del llamado)
  }
}

describe "Test de portales y combate sin loop" {

  test "Acceso a Sala del Boss depende de tieneAccesoASalaBoss()" {
    const h = new Personaje(position = game.center())
    assert.equals(false, h.tieneAccesoASalaBoss())

    h.ganarExp(h.expSiguienteNivel())  // -> 2
    h.ganarExp(h.expSiguienteNivel())  // -> 3

    assert.equals(true, h.tieneAccesoASalaBoss())
  }

  test "Portal con condicion por tieneAccesoASalaBoss() no rompe y respeta gating" {
    const h = new Personaje(position = game.center())
    const portalBoss = new Portal(
      position=game.at(15,4),
      destino=salaDelBoss,
      condicion={ pj => pj.tieneAccesoASalaBoss() }
    )

    mundo.cambiarArea(puebloDelRey)
    portalBoss.fueTocadoPor(h)   // nivel 1 → no pasa (pero no podemos observar el área), sólo que no rompa

    h.ganarExp(h.expSiguienteNivel())  // 2
    h.ganarExp(h.expSiguienteNivel())  // 3

    portalBoss.fueTocadoPor(h)   // ahora debería pasar → nuevamente, sólo chequeo que no rompa
    assert.equals(true, true)
  }

  test "Usar habilidad del héroe baja vida del otro (P vs P, sin loop de combate)" {
    const h = new Personaje(position = game.center())
    const otro = new Personaje(position = game.center())

    h.habilidades().clear()
    h.habilidades().add(new HabilidadAtaque(nombre="Golpe UTN", danio=5, tipoDanio="fisico", costoMana=0))

    const vidaAntes = otro.vida()
    h.usarHabilidad(h.habilidades().first(), otro)

    assert.equals(true, otro.vida() < vidaAntes)
  }

  test "Usar ítem del héroe cura y se consume (sin loop de combate)" {
    const h = mundo.heroe()
    h.vida(50)
    h.inventario().clear()
    h.inventario().add(new Pocion(nombre="Poción 30", curacion=30))

    h.usarItem(h.inventario().first())

    assert.equals(80, h.vida())
    assert.equals(0, h.inventario().size())
  }

  test "Volver a exploración deja estado 'explorando'" {
  mundo.cambiarArea(bosqueDeMonstruos)

  mundo.volverAExploracion()

  assert.equals("explorando", mundo.estadoJuego())
}

  test "Daño letal al héroe → vida=0 y no-vivo" {
    const h = new Personaje(position = game.center())
    h.recibirDaño(9999)
    assert.equals(0, h.vida())
    assert.equals(false, h.estaVivo())
  }
}
